#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	Если ЗначениеЗаполнено(Объект.ЦветRGB) Тогда
		Фон = СтрРазделить(Объект.ЦветRGB, ",");	

		Элементы.ФормаВыбратьЦвет.ЦветФона = Новый Цвет(Фон[0], Фон[1], Фон[2]);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ЦветТекстаRGB) Тогда
		ЦветТекста = СтрРазделить(Объект.ЦветТекстаRGB, ",");	

		Элементы.ФормаВыбратьЦвет.ЦветТекста = Новый Цвет(ЦветТекста[0], ЦветТекста[1], ЦветТекста[2]);	
	КонецЕсли; 		
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЗакрытПриИзменении(Элемент)
	Если Объект.Закрыт Тогда
		Объект.Новый = Ложь;		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НовыйПриИзменении(Элемент)
	Если Объект.Новый Тогда
		Объект.Закрыт = Ложь;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ВыбратьЦвет(Команда)
    ОткрытьФормуВыбораЦвета("ЦветHEX", "ЦветRGB", "ЦветТекстаRGB"); 
КонецПроцедуры
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПослеЗакрытияФормыВыбораЦвета(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено ИЛИ НЕ ЗначениеЗаполнено(ВыбранноеЗначение) 
		ИЛИ ВыбранноеЗначение = КодВозвратаДиалога.Отмена Тогда
		Возврат;	
	КонецЕсли; 	
	
	// Цвет фона/текста в HEX	
	ИмяРеквизитаЦветаНЕХ 			= ДополнительныеПараметры.ИмяРеквизитаЦветаHEX;
	Объект[ИмяРеквизитаЦветаНЕХ] 	= ВыбранноеЗначение.ЦветHEX;         
	
	// Цвет фона/текста в RGB (для фона элемента формы)
	
	ЦветФона = Новый Цвет(ВыбранноеЗначение.ЦветRGB.R, ВыбранноеЗначение.ЦветRGB.G, ВыбранноеЗначение.ЦветRGB.B);  
	
	Элементы.ФормаВыбратьЦвет.ЦветФона = ЦветФона;
		                      
	МассивЧастейЦвета = Новый Массив;
	МассивЧастейЦвета.Добавить(ВыбранноеЗначение.ЦветRGB.R);
	МассивЧастейЦвета.Добавить(ВыбранноеЗначение.ЦветRGB.G);
	МассивЧастейЦвета.Добавить(ВыбранноеЗначение.ЦветRGB.B); 
	
	Объект[ДополнительныеПараметры.ИмяРеквизитаЦветаRGB] = СтрСоединить(МассивЧастейЦвета, ",");
	
	МассивЧастейЦвета = Новый Массив;
	МассивЧастейЦвета.Добавить(ВыбранноеЗначение.ЦветТекстаRGB.R);
	МассивЧастейЦвета.Добавить(ВыбранноеЗначение.ЦветТекстаRGB.G);
	МассивЧастейЦвета.Добавить(ВыбранноеЗначение.ЦветТекстаRGB.B);  
	
	Объект[ДополнительныеПараметры.ИмяРеквизитаЦветаТекстаRGB] = СтрСоединить(МассивЧастейЦвета, ",");
	
	ЦветТекста = Новый Цвет(ВыбранноеЗначение.ЦветТекстаRGB.R, 
		ВыбранноеЗначение.ЦветТекстаRGB.G, ВыбранноеЗначение.ЦветТекстаRGB.B);  
	
	Элементы.ФормаВыбратьЦвет.ЦветТекста = ЦветТекста;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораЦвета(ИмяРеквизитаЦветаНЕХ, ИмяРеквизитаЦветаРГБ = "", ИмяРеквизитаЦветаТекстаРГБ = "")
	ДополнительныеПараметры = Новый Структура("ИмяРеквизитаЦветаHEX, ИмяРеквизитаЦветаRGB, ИмяРеквизитаЦветаТекстаRGB", 
	ИмяРеквизитаЦветаНЕХ, ИмяРеквизитаЦветаРГБ, ИмяРеквизитаЦветаТекстаРГБ);
	ПослеЗакрытияФормыВыбораЦвета = Новый ОписаниеОповещения("ВыполнитьПослеЗакрытияФормыВыбораЦвета", ЭтотОбъект,
		ДополнительныеПараметры);
	ПараметрФормы = Новый Структура("Цвет", Объект[ИмяРеквизитаЦветаНЕХ]);
	ОткрытьФорму("ОбщаяФорма.Центр_ФормаВыбораЦвета", ПараметрФормы, ЭтотОбъект, , , , ПослеЗакрытияФормыВыбораЦвета);

КонецПроцедуры
#КонецОбласти