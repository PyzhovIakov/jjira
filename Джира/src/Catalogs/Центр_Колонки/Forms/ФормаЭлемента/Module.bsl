#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Не Доски.Количество() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Доски не введены");
		Отказ=Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Набор = РегистрыСведений.Центр_НастройкиПользователейКолонокКанБан.СоздатьНаборЗаписей();
	Набор.Отбор.Колонка.Установить(ТекущийОбъект.Ссылка);
	Набор.Отбор.Пользователь.Установить(ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка"));
	Набор.Прочитать();
	Набор.Очистить();
	Для Каждого СтрДоска Из Доски Цикл
		Запись = Набор.Добавить();
		Запись.Пользователь = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		Запись.Колонка = ТекущийОбъект.Ссылка;
		Запись.Доска = СтрДоска.Доска;
		Запись.НастройкиКолонки = Новый ХранилищеЗначения(Колонка.КомпоновщикНастроек.ПолучитьНастройки());
	КонецЦикла;
	Набор.Записать();
КонецПроцедуры
#КонецОбласти


#Область  СлужебныеПроцедурыИФункции
&НаСервере
Процедура ПриОткрытииНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Центр_НастройкиПользователейКолонокКанБан.НастройкиКолонки,
	|	Центр_НастройкиПользователейКолонокКанБан.Колонка,
	|	Центр_НастройкиПользователейКолонокКанБан.Пользователь
	|ИЗ
	|	РегистрСведений.Центр_НастройкиПользователейКолонокКанБан КАК Центр_НастройкиПользователейКолонокКанБан
	|ГДЕ
	|	Центр_НастройкиПользователейКолонокКанБан.Пользователь = Значение(Справочник.Пользователи.ПустаяСсылка)
	|	И Центр_НастройкиПользователейКолонокКанБан.Колонка = &Колонка";

	Запрос.УстановитьПараметр("Колонка", Объект.Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка= РезультатЗапроса.Выбрать();
	
	
	Если Выборка.Следующий() Тогда
		Колонка.КомпоновщикНастроек.ЗагрузитьНастройки(Выборка.НастройкиКолонки.Получить());
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	Центр_НастройкиПользователейКолонокКанБан.Доска
				   |ИЗ
				   |	РегистрСведений.Центр_НастройкиПользователейКолонокКанБан КАК Центр_НастройкиПользователейКолонокКанБан
				   |ГДЕ
				   |	Центр_НастройкиПользователейКолонокКанБан.Пользователь = Значение(Справочник.Пользователи.ПустаяСсылка)
				   |	И Центр_НастройкиПользователейКолонокКанБан.Колонка = &Колонка";

	Запрос.УстановитьПараметр("Колонка", Объект.Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка= РезультатЗапроса.Выбрать();
	Доски.Очистить();
	Пока Выборка.Следующий() Цикл
		НоваяДоска = Доски.Добавить();
		НоваяДоска.Доска = Выборка.Доска;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти