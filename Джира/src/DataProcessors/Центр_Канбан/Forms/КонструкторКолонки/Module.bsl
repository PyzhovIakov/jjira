#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Доска") Тогда
		Если ЗначениеЗаполнено(Параметры.Доска) Тогда
			Доска=Параметры.Доска;
		КонецЕсли;
	КонецЕсли;

	Если Параметры.Свойство("Номер") Тогда
		Если ЗначениеЗаполнено(Параметры.Номер) Тогда
			Номер=Параметры.Номер;
		КонецЕсли;
	КонецЕсли;

	Если Параметры.Свойство("Наименование") Тогда
		Если ЗначениеЗаполнено(Параметры.Наименование) Тогда
			Наименование = Параметры.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("БезНастроек") Тогда
		Если ЗначениеЗаполнено(Параметры.БезНастроек) Тогда
			БезНастроек = Параметры.БезНастроек;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьПоУмолчанию(Команда)
	Если ЗначениеЗаполнено(Наименование) Тогда
		СохранитьПоУмолчаниюНаСервере();
		ЭтотОбъект.Закрыть();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Введите наименование");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКонструктор(Команда)
	ЭтотОбъект.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	Если ЗначениеЗаполнено(Наименование) Тогда
		ПроверитьКолонкиПользователя();
		СохранитьНаСервере();
		ЭтотОбъект.Закрыть();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Введите наименование");
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьКолонкиПользователя()
	Пользователь = Пользователи.АвторизованныйПользователь();

	Набор = РегистрыСведений.Центр_НастройкиПользователейКолонокКанБан.СоздатьНаборЗаписей();
	Набор.Отбор.Доска.Установить(Доска);
	Набор.Отбор.Пользователь.Установить(Пользователь);
	Набор.Прочитать();

	Если Не Набор.Количество() Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_НастройкиПользователейКолонокКанБан.Номер,
		|	Центр_НастройкиПользователейКолонокКанБан.НастройкиКолонки,
		|	Центр_НастройкиПользователейКолонокКанБан.Наименование
		|ИЗ
		|	РегистрСведений.Центр_НастройкиПользователейКолонокКанБан КАК Центр_НастройкиПользователейКолонокКанБан
		|ГДЕ
		|	Центр_НастройкиПользователейКолонокКанБан.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|	И Центр_НастройкиПользователейКолонокКанБан.Доска = &Доска";

		Запрос.УстановитьПараметр("Доска", Доска);

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл
			Запись = Набор.Добавить();
			Запись.Доска = Доска;
			Запись.Пользователь = Пользователь;
			Запись.Наименование = Выборка.Наименование;
			Запись.Номер = Выборка.Номер;
			Запись.НастройкиКолонки = Выборка.НастройкиКолонки;
		КонецЦикла;

	КонецЕсли;

	Набор.Записать();
КонецПроцедуры

&НаСервере
Процедура СохранитьПоУмолчаниюНаСервере()

	Набор = РегистрыСведений.Центр_НастройкиПользователейКолонокКанБан.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка"));
	Набор.Отбор.Доска.Установить(Доска);
	Набор.Прочитать();

	Запись = Набор.Добавить();
	Запись.Пользователь = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	Запись.Доска = Доска;
	Запись.Номер = Набор.Количество();
	Запись.Наименование = Наименование;
	Запись.НастройкиКолонки = Новый ХранилищеЗначения(Колонка.КомпоновщикНастроек.ПолучитьНастройки());

	Набор.Записать();
КонецПроцедуры
&НаСервере
Процедура СохранитьНаСервере()
	Набор = РегистрыСведений.Центр_НастройкиПользователейКолонокКанБан.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(Пользователи.АвторизованныйПользователь());
	Набор.Отбор.Доска.Установить(Доска);
	Набор.Отбор.Номер.Установить(Номер);
	Набор.Прочитать();
	Если Набор.Количество() Тогда
		Запись = Набор[0];
		Запись.НастройкиКолонки = Новый ХранилищеЗначения(Колонка.КомпоновщикНастроек.ПолучитьНастройки());
		Запись.Наименование = Наименование;
	Иначе
		Запись = Набор.Добавить();
		Запись.Пользователь = Пользователи.АвторизованныйПользователь();
		Запись.Доска = Доска;
		Запись.Номер = Номер;
		Запись.Наименование = Наименование;
		Запись.НастройкиКолонки = Новый ХранилищеЗначения(Колонка.КомпоновщикНастроек.ПолучитьНастройки());
	КонецЕсли;
	Набор.Записать();
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	Если Не БезНастроек Тогда
		Настройки = Параметры.НастройкиКолонки.ПолучитьНастройки();
		Колонка.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти