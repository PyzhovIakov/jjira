
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПолучитьВыбраннуюДоску();
	ОтобразитьДоску();
КонецПроцедуры

#КонецОбласти
&НаКлиенте
Процедура ВыбраннаяДоскаПриИзменении(Элемент)
	ОтобразитьДоску();
	СохранитьИзменениеВыбраннойДоски();
КонецПроцедуры


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СохранитьНастройкиОтборов(Команда)
	СохранитьНастройкиОтборовНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура СброситьНастройки(Команда)
	СброситьНастройкиНаСервере();
КонецПроцедуры
#КонецОбласти

//#Область ОбработчикиСобытийЭлементовТаблицыФормы


//#КонецОбласти




#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтобразитьДоску()
	//Очистка
	МассивИменЭлементов = Новый Массив;
	МассивИменРеквизитов = Новый Массив;
	МассивИменКоманд = Новый Массив;
	Для к = 1 По КоличествоКолонок Цикл
		МассивИменЭлементов.Добавить("ГруппаКолонки" + к);
		МассивИменРеквизитов.Добавить("Колонка" + к);
		МассивИменКоманд.Добавить("НастройкиКолонки" + к);
	КонецЦикла;
	Центр_ЭлементыФормы.УдлЭлементы(ЭтотОбъект, МассивИменЭлементов);
	Центр_ЭлементыФормы.УдлРеквизиты(ЭтотОбъект, МассивИменРеквизитов);
	Центр_ЭлементыФормы.УдлКоманды(ЭтотОбъект, МассивИменКоманд);
	
	Колонки.Очистить();
	
	//создание объектов
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Центр_НастройкиПользователейКолонокКанБан.НастройкиКолонки,
	|	Центр_НастройкиПользователейКолонокКанБан.Колонка КАК Колонка
	|ПОМЕСТИТЬ вт_НастройкиПоУмолчанию
	|ИЗ
	|	РегистрСведений.Центр_НастройкиПользователейКолонокКанБан КАК Центр_НастройкиПользователейКолонокКанБан
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Центр_Колонки КАК Центр_Колонки
	|		ПО Центр_НастройкиПользователейКолонокКанБан.Колонка = Центр_Колонки.Ссылка
	|		И Центр_Колонки.ПоУмолчанию
	|ГДЕ
	|	Центр_НастройкиПользователейКолонокКанБан.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|	И Центр_НастройкиПользователейКолонокКанБан.Доска = &Доска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Центр_НастройкиПользователейКолонокКанБан.Колонка,
	|	Центр_НастройкиПользователейКолонокКанБан.НастройкиКолонки
	|ПОМЕСТИТЬ вт_НастройкиПользователя
	|ИЗ
	|	РегистрСведений.Центр_НастройкиПользователейКолонокКанБан КАК Центр_НастройкиПользователейКолонокКанБан
	|ГДЕ
	|	Центр_НастройкиПользователейКолонокКанБан.Пользователь = &Пользователь
	|	И Центр_НастройкиПользователейКолонокКанБан.Доска = &Доска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(вт_НастройкиПользователя.Колонка, вт_НастройкиПоУмолчанию.Колонка) КАК Колонка,
	|	ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(вт_НастройкиПользователя.Колонка, вт_НастройкиПоУмолчанию.Колонка)) КАК КолонкаПредставление,
	|	вт_НастройкиПользователя.НастройкиКолонки КАК НастройкиПользователя,
	|	вт_НастройкиПоУмолчанию.НастройкиКолонки КАК НастройкиПоУмолчанию
	|ИЗ
	|	вт_НастройкиПоУмолчанию КАК вт_НастройкиПоУмолчанию
	|		ПОЛНОЕ СОЕДИНЕНИЕ вт_НастройкиПользователя КАК вт_НастройкиПользователя
	|		ПО вт_НастройкиПоУмолчанию.Колонка = вт_НастройкиПользователя.Колонка";

	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());
	Запрос.УстановитьПараметр("Доска",ВыбраннаяДоска);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();

	КоличествоКолонок = Выборка.Количество();

	ТекстЗапросаДС = "ВЫБРАТЬ
	|	Центр_Задача.Ссылка как Задача,
	|	Центр_Задача.ВерсияДанных,
	|	Центр_Задача.ПометкаУдаления,
	|	Центр_Задача.Номер,
	|	Центр_Задача.Дата,
	|	Центр_Задача.Проведен,
	|	Центр_Задача.Наименование,
	|	Центр_Задача.ОписаниеЗадачи,
	|	Центр_Задача.ВидЗадачи,
	|	Центр_Задача.Постановщик,
	|	Центр_Задача.Дедлайн,
	|	Центр_Задача.Длительность,
	|	Центр_Задача.Приоритет,
	|	Центр_Задача.Автор,
	|	Центр_Задача.ГлавнаяЗадача,
	|	Центр_Задача.Наблюдатели.(
	|		Ссылка,
	|		НомерСтроки,
	|		Пользовалель),
	|	Центр_Задача.Кандидаты.(
	|		Ссылка,
	|		НомерСтроки,
	|		Кандидат)
	|ИЗ
	|	Документ.Центр_Задача КАК Центр_Задача";

	Индекс=1;

	СтруктураКолонок = Новый Структура;
	СтруктураКолонок.Вставить("Задача", "Задача");
	СтруктураКолонок.Вставить("Наименование", "Наименование");
	КартинкаНастроек = БиблиотекаКартинок.НастроитьСостоянияОригиналаПервичногоДокумента;

	СвойствоКнопок = Новый Структура;
	СвойствоКнопок.Вставить("Картинка", КартинкаНастроек);
	СвойствоКнопок.Вставить("Отображение", ОтображениеКнопки.Картинка);
	
	СтруктураСвойствДС = Новый Структура;
	СтруктураСвойствДС.Вставить("ПоложениеКоманднойПанели", ПоложениеКоманднойПанелиЭлементаФормы.Нет);
	СтруктураСвойствДС.Вставить("ТолькоПросмотр", Истина);

	Пока Выборка.Следующий() Цикл
		НоваяКолонка = Колонки.Добавить();
		НоваяКолонка.ЗаголовокКолонки = "Колонка" + Индекс;
		НоваяКолонка.Колонка = Выборка.Колонка;

		ГруппаКолонки = Центр_ЭлементыФормы.СздГруппаОбычная(ЭтотОбъект, "ГруппаКолонки" + Индекс,
			Элементы.ГруппаОсновногоБлока, Выборка.КолонкаПредставление, 1);

		Центр_ЭлементыФормы.СоздатьКоманду(ЭтотОбъект, "НастройкиКолонки" + Индекс, "Настройки",
			"ОткрытьНастройкиДинамическогоСпискаКолонки");
		КонпкаНстроекКолонки = Центр_ЭлементыФормы.СздКнопка(ЭтотОбъект, "НастройкиКолонкиКнопка" + Индекс,
			ГруппаКолонки, "Настройки", "НастройкиКолонки" + Индекс, , СвойствоКнопок);
		ТаблицаДСКолонки = Центр_ЭлементыФормы.СздДинамическийСписок(ЭтотОбъект, "Колонка" + Индекс, ГруппаКолонки,
			ТекстЗапросаДС, , "Документ.Центр_Задача", СтруктураКолонок, СтруктураСвойствДС, , , );
		Если ЗначениеЗаполнено(Выборка.НастройкиПользователя) Тогда
			ЭтотОбъект["Колонка" + Индекс].КомпоновщикНастроек.ЗагрузитьНастройки(
				Выборка.НастройкиПользователя.Получить());
		Иначе
			ЭтотОбъект["Колонка" + Индекс].КомпоновщикНастроек.ЗагрузитьНастройки(
				Выборка.НастройкиПоУмолчанию.Получить());
		КонецЕсли;
		Индекс=Индекс + 1;
	КонецЦикла;
КонецПроцедуры


&НаСервере
Процедура ПолучитьВыбраннуюДоску()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Центр_НастройкиПользователяКанБан.Доска
	|ИЗ
	|	РегистрСведений.Центр_НастройкиПользователяКанБан КАК Центр_НастройкиПользователяКанБан
	|ГДЕ
	|	Центр_НастройкиПользователяКанБан.Пользователь = &Пользователь";

	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		ВыбраннаяДоска = Выборка.Доска;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВыбраннаяДоска) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Центр_НастройкиПользователейКолонокКанБан.Доска
		|ИЗ
		|	РегистрСведений.Центр_НастройкиПользователейКолонокКанБан КАК Центр_НастройкиПользователейКолонокКанБан
		|ГДЕ
		|	Центр_НастройкиПользователейКолонокКанБан.Пользователь = &Пользователь";
		Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());

		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Запрос.УстановитьПараметр("Пользователь", ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка"));
			РезультатЗапроса = Запрос.Выполнить();
		КонецЕсли;
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ВыбраннаяДоска = Выборка.Доска;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СохранитьИзменениеВыбраннойДоски()
	Набор = РегистрыСведений.Центр_НастройкиПользователяКанБан.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(Пользователи.АвторизованныйПользователь());
	Набор.Прочитать();
	Если Набор.Количество() тогда
		Запись = Набор[0];
		Запись.Доска = ВыбраннаяДоска;
	иначе
		Запись = Набор.Добавить();
		Запись.Пользователь = Пользователи.АвторизованныйПользователь();
		Запись.Доска = ВыбраннаяДоска;
	КонецЕсли;
	Набор.Записать();	
КонецПроцедуры

&НаСервере
Процедура НазначитьНастройкиКомпоновщика(Источник)
	НастройкиКомпоновкиДанных = ЭтотОбъект[Источник].КомпоновщикНастроек.ПолучитьИсточникДоступныхНастроек();
	НастройкиКолонки.Инициализировать(НастройкиКомпоновкиДанных);

	НастройкиКолонки.ЗагрузитьНастройки(ЭтотОбъект[Источник].КомпоновщикНастроек.ПолучитьНастройки());
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиДинамическогоСпискаКолонки(Команда) Экспорт
	ИмяКолонки = "Колонка" + Прав(Команда.Имя, 1);
	НазначитьНастройкиКомпоновщика(ИмяКолонки);
	Элементы.НастройкиКолонкиПользовательскиеНастройки.Обновить();
	Элементы.ГруппаШапка.Показать();
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтборовНаСервере()
	ЭтотОбъект[ИмяКолонки].КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКолонки.ПолучитьНастройки());

	МассКолонки = Колонки.НайтиСтроки(Новый структура("ЗаголовокКолонки", ИмяКолонки));
	Если МассКолонки.Количество() Тогда
		Набор = РегистрыСведений.Центр_НастройкиПользователейКолонокКанБан.СоздатьНаборЗаписей();
		Набор.Отбор.Доска.Установить(ВыбраннаяДоска);
		Набор.Отбор.Колонка.Установить(МассКолонки[0].Колонка);
		Набор.Отбор.Пользователь.Установить(Пользователи.АвторизованныйПользователь());
		Набор.Прочитать();

		Если Набор.Количество() Тогда
			Запись = Набор[0];
			Запись.НастройкиКолонки = Новый ХранилищеЗначения(НастройкиКолонки.ПолучитьНастройки());
		Иначе
			Запись = Набор.Добавить();
			Запись.Доска = ВыбраннаяДоска;
			Запись.Колонка = МассКолонки[0].Колонка;
			Запись.Пользователь = Пользователи.АвторизованныйПользователь();
			Запись.НастройкиКолонки = Новый ХранилищеЗначения(НастройкиКолонки.ПолучитьНастройки());
		КонецЕсли;
		Набор.Записать();
	КонецЕсли;

	НастройкиКолонки.Восстановить();
	ИмяКолонки="";
	Элементы.ГруппаШапка.Скрыть();
КонецПроцедуры

&НаСервере
Процедура СброситьНастройкиНаСервере()

	МассКолонки = Колонки.НайтиСтроки(Новый структура("ЗаголовокКолонки", ИмяКолонки));
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_НастройкиПользователейКолонокКанБан.НастройкиКолонки
		|ИЗ
		|	РегистрСведений.Центр_НастройкиПользователейКолонокКанБан КАК Центр_НастройкиПользователейКолонокКанБан
		|ГДЕ
		|	Центр_НастройкиПользователейКолонокКанБан.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|	И Центр_НастройкиПользователейКолонокКанБан.Колонка = &Колонка
		|	И Центр_НастройкиПользователейКолонокКанБан.Доска = &Доска";
	
	Запрос.УстановитьПараметр("Колонка", МассКолонки[0].Колонка);
	Запрос.УстановитьПараметр("Доска", ВыбраннаяДоска);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НастройкиПоУмолчаниюИзБазы = неопределено;
	Если  Выборка.Следующий() Тогда
		НастройкиПоУмолчаниюИзБазы = Выборка.НастройкиКолонки;
		ЭтотОбъект[ИмяКолонки].КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиПоУмолчаниюИзБазы.Получить());
	КонецЕсли;

	
	Если МассКолонки.Количество() Тогда
		Набор = РегистрыСведений.Центр_НастройкиПользователейКолонокКанБан.СоздатьНаборЗаписей();
		Набор.Отбор.Доска.Установить(ВыбраннаяДоска);
		Набор.Отбор.Колонка.Установить(МассКолонки[0].Колонка);
		Набор.Отбор.Пользователь.Установить(Пользователи.АвторизованныйПользователь());
		Набор.Прочитать();

		Если Набор.Количество() Тогда
			Запись = Набор[0];
			Запись.НастройкиКолонки = Новый ХранилищеЗначения(НастройкиПоУмолчаниюИзБазы);
		Иначе
			Запись = Набор.Добавить();
			Запись.Доска = ВыбраннаяДоска;
			Запись.Колонка = МассКолонки[0].Колонка;
			Запись.Пользователь = Пользователи.АвторизованныйПользователь();
			Запись.НастройкиКолонки = Новый ХранилищеЗначения(НастройкиПоУмолчаниюИзБазы);
		КонецЕсли;
		Набор.Записать();
	КонецЕсли;
	
	НастройкиКолонки.Инициализировать(Неопределено);
	НастройкиКолонки.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.Полное);
	
	ИмяКолонки="";
	Элементы.ГруппаШапка.Скрыть();
КонецПроцедуры
#КонецОбласти