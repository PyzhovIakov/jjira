
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОсновнаяЗадача = Неопределено;
	Если Параметры.Свойство("ОсновнаяЗадача", ОсновнаяЗадача) Тогда
		Задача = ОсновнаяЗадача;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Центр_ВопросыПоЗадачам.Отправитель,
			|	МАКСИМУМ(Центр_ВопросыПоЗадачам.Дата) КАК Дата
			|ПОМЕСТИТЬ вт_ПоследнийВопрос
			|ИЗ
			|	РегистрСведений.Центр_ВопросыПоЗадачам КАК Центр_ВопросыПоЗадачам
			|ГДЕ
			|	Центр_ВопросыПоЗадачам.Задача = &Задача
			|	И Центр_ВопросыПоЗадачам.Адресат = &Адресат
			|	И Центр_ВопросыПоЗадачам.ВидВопроса = ЗНАЧЕНИЕ(Перечисление.Центр_ВидыВопросов.Вопрос)
			|СГРУППИРОВАТЬ ПО
			|	Центр_ВопросыПоЗадачам.Отправитель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Центр_ВопросыПоЗадачам.ТекстВопроса,
			|	вт_ПоследнийВопрос.Отправитель,
			|	вт_ПоследнийВопрос.Дата
			|ИЗ
			|	РегистрСведений.Центр_ВопросыПоЗадачам КАК Центр_ВопросыПоЗадачам
			|		ПРАВОЕ СОЕДИНЕНИЕ вт_ПоследнийВопрос КАК вт_ПоследнийВопрос
			|		ПО вт_ПоследнийВопрос.Отправитель = Центр_ВопросыПоЗадачам.Отправитель
			|		И вт_ПоследнийВопрос.Дата = Центр_ВопросыПоЗадачам.Дата
			|		И Центр_ВопросыПоЗадачам.Задача = &Задача
			|		И Центр_ВопросыПоЗадачам.Адресат = &Адресат
			|		И Центр_ВопросыПоЗадачам.ВидВопроса = ЗНАЧЕНИЕ(Перечисление.Центр_ВидыВопросов.Вопрос)";
		
		Запрос.УстановитьПараметр("Задача", Задача);
		Запрос.УстановитьПараметр("Адресат", Пользователи.АвторизованныйПользователь());
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ТекстВопроса) Тогда
			Адресат = Выборка.Отправитель;
			ТекстВопроса = Выборка.ТекстВопроса;
			Дата = Выборка.Дата;
		Иначе 
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Вопросы к вам по данной задаче отсутствуют";
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОтправитьВопрос(Команда)
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(ТекстОтвета) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Введите текст вопроса";
		Сообщение.Поле = "ТекстНовогоВопроса";
		Сообщение.Сообщить();
	КонецЕсли;	
	
	Если Не Отказ Тогда
		ОтправитьВопросНаСервере();
		Центр_ОбработкаТриггеров.ТриггерОтветНаВопросПоЗадаче(Задача);
		ВладелецФормы.ПослеОтветаНаВопрос();
		Закрыть();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

//@skip-check module-structure-top-region
#Область ВспомогательныеПроцедурыИФункции
&НаСервере
Процедура ОтправитьВопросНаСервере()
	МенеджерЗаписи = РегистрыСведений.Центр_ВопросыПоЗадачам.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Адресат = Адресат;
	МенеджерЗаписи.Задача = Задача;
	МенеджерЗаписи.Отправитель = ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.ТекстВопроса = ТекстОтвета;
	МенеджерЗаписи.ВидВопроса = Перечисления.Центр_ВидыВопросов.Ответ;
	МенеджерЗаписи.Записать();
КонецПроцедуры
#КонецОбласти
