
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОсновнаяЗадача = Неопределено;
	Если Параметры.Свойство("ОсновнаяЗадача", ОсновнаяЗадача) Тогда
		ОтборЗадача = Вопросы.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ПолеОтбора = Новый ПолеКомпоновкиДанных("Задача");
		ОтборЗадача.ЛевоеЗначение = ПолеОтбора;
		ОтборЗадача.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЗадача.ПравоеЗначение = ОсновнаяЗадача;
		
		Задача = ОсновнаяЗадача;
		СписокДоступныхАдресатов = АдресатыВыбраннойЗадачи();
	
		Для Каждого АдресатСписка Из СписокДоступныхАдресатов Цикл
			Элементы.Адресат.СписокВыбора.Добавить(АдресатСписка);
		КонецЦикла;
	КонецЕсли;
	

	ОтборЗадача = Вопросы.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ПолеОтбора = Новый ПолеКомпоновкиДанных("Отправитель");
	ОтборЗадача.ЛевоеЗначение = ПолеОтбора;
	ОтборЗадача.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЗадача.ПравоеЗначение = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура НастройкиСпискаВопросов(Команда)
	НастройкиСпискаВопросовНаСервере();
	
	Элементы.ИнтерфейсКомпоновкиПользовательскиеНастройки.Обновить();
	Элементы.ГруппаНастроекСписка.Показать();
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	ПрименитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВопрос(Команда)
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Задача) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не выбрана задача";
		Сообщение.Поле = "Задача";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Адресат) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не выбран адресат";
		Сообщение.Поле = "Адресат";
		Сообщение.Сообщить();
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ТекстНовогоВопроса) Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Введите текст вопроса";
		Сообщение.Поле = "ТекстНовогоВопроса";
		Сообщение.Сообщить();
	КонецЕсли;	
	
	Если Не Отказ Тогда
		ОтправитьВопросНаСервере();
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СброситьНастройкиСписка(Команда)
	СброситьНастройкиСпискаНаСервере();
КонецПроцедуры
#КонецОбласти

//@skip-check module-structure-top-region
#Область ОбработчикиСобытийЭлементоыФормы
//@skip-check module-structure-form-event-regions
&НаКлиенте
Процедура ЗадачаПриИзменении(Элемент)
	СписокДоступныхАдресатов = АдресатыВыбраннойЗадачи();
	
	Для Каждого АдресатСписка Из СписокДоступныхАдресатов Цикл
		Элементы.Адресат.СписокВыбора.Добавить(АдресатСписка);
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

//@skip-check module-structure-top-region
#Область ВспомогательныеПроцедурыИФункции
&НаСервере
Функция АдресатыВыбраннойЗадачи()
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ГруппыДоступаКЗадачамПользователи.Пользователь
		|ПОМЕСТИТЬ вт_КандидатыИзГрупп
		|ИЗ
		|	Справочник.Центр_ГруппыДоступаКЗадачам.Пользователи КАК Центр_ГруппыДоступаКЗадачамПользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Центр_Задача.Кандидаты КАК Центр_ЗадачаКандидаты
		|		ПО Центр_ГруппыДоступаКЗадачамПользователи.Ссылка = Центр_ЗадачаКандидаты.Кандидат
		|ГДЕ
		|	Центр_ЗадачаКандидаты.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	Центр_ГруппыДоступаКЗадачамПользователи.Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Центр_ЗадачаНаблюдатели.Пользовалель КАК Пользователь
		|ИЗ
		|	Документ.Центр_Задача.Наблюдатели КАК Центр_ЗадачаНаблюдатели
		|ГДЕ
		|	Центр_ЗадачаНаблюдатели.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	Центр_ЗадачаНаблюдатели.Пользовалель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Центр_ЗадачаКандидаты.Кандидат ССЫЛКА Справочник.Пользователи
		|			ТОГДА Центр_ЗадачаКандидаты.Кандидат
		|	КОНЕЦ
		|ИЗ
		|	Документ.Центр_Задача.Кандидаты КАК Центр_ЗадачаКандидаты
		|ГДЕ
		|	Центр_ЗадачаКандидаты.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА Центр_ЗадачаКандидаты.Кандидат ССЫЛКА Справочник.Пользователи
		|			ТОГДА Центр_ЗадачаКандидаты.Кандидат
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Центр_ИсполнителиЗадачСрезПоследних.Исполнитель
		|ИЗ
		|	РегистрСведений.Центр_ИсполнителиЗадач.СрезПоследних(, Задача = &Ссылка) КАК Центр_ИсполнителиЗадачСрезПоследних
		|СГРУППИРОВАТЬ ПО
		|	Центр_ИсполнителиЗадачСрезПоследних.Исполнитель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Центр_Задача.Автор
		|ИЗ
		|	Документ.Центр_Задача КАК Центр_Задача
		|ГДЕ
		|	Центр_Задача.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	Центр_Задача.Автор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Центр_Задача.Постановщик ССЫЛКА Справочник.Пользователи
		|			ТОГДА Центр_Задача.Постановщик
		|	КОНЕЦ
		|ИЗ
		|	Документ.Центр_Задача КАК Центр_Задача
		|ГДЕ
		|	Центр_Задача.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА Центр_Задача.Постановщик ССЫЛКА Справочник.Пользователи
		|			ТОГДА Центр_Задача.Постановщик
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_КандидатыИзГрупп.Пользователь
		|ИЗ
		|	вт_КандидатыИзГрупп КАК вт_КандидатыИзГрупп
		|СГРУППИРОВАТЬ ПО
		|	вт_КандидатыИзГрупп.Пользователь";
	
	Запрос.УстановитьПараметр("Ссылка", Задача);
	
	Выборка =  Запрос.Выполнить().Выбрать();
	
	МассивКандидатов = Новый Массив();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Пользователь) Тогда
			МассивКандидатов.Добавить(Выборка.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивКандидатов;
КонецФункции

&НаСервере
Процедура ОтправитьВопросНаСервере()
	МенеджерЗаписи = РегистрыСведений.Центр_ВопросыПоЗадачам.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Адресат = Адресат;
	МенеджерЗаписи.Задача = Задача;
	МенеджерЗаписи.Отправитель = ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.ТекстВопроса = ТекстНовогоВопроса;
	МенеджерЗаписи.Записать();
КонецПроцедуры

&НаСервере
Процедура НастройкиСпискаВопросовНаСервере()
	НастройкиКомпоновкиДанных = Вопросы.КомпоновщикНастроек.ПолучитьИсточникДоступныхНастроек();
	ИнтерфейсКомпоновки.Инициализировать(НастройкиКомпоновкиДанных);

	ИнтерфейсКомпоновки.ЗагрузитьНастройки(Вопросы.КомпоновщикНастроек.ПолучитьНастройки());
КонецПроцедуры

&НаСервере
Процедура СброситьНастройкиСпискаНаСервере()
	ИнтерфейсКомпоновки.Инициализировать(Неопределено);
	Вопросы.КомпоновщикНастроек.ЗагрузитьНастройки(ИнтерфейсКомпоновки.ПолучитьНастройки());
	Элементы.ГруппаНастроекСписка.Скрыть();
КонецПроцедуры

&НаСервере
Процедура ПрименитьНаСервере()
	Вопросы.КомпоновщикНастроек.ЗагрузитьНастройки(ИнтерфейсКомпоновки.ПолучитьНастройки());
	Элементы.ГруппаНастроекСписка.Скрыть();
КонецПроцедуры
#КонецОбласти
