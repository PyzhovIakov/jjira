#Область ОбработчикиСобытий
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Центр_Задача") Тогда
		Часы = 8;
		Основание = ДанныеЗаполнения;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Движения.Центр_ЧасыТрудозатрат.Записывать = Истина;	
	Движение = Движения.Центр_ЧасыТрудозатрат.Добавить();
	Движение.Период = Дата;
	Движение.Исполнитель = ПараметрыСеанса.ТекущийПользователь;
	Движение.Часы = Часы;	
	Движения.Центр_ЧасыТрудозатрат.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(Центр_ЧасыТрудозатрат.Часы) КАК Часы,
	|	Центр_ТрудозатратыПоЗадаче.Основание
	|ПОМЕСТИТЬ вт_ЧасыПоЗадаче
	|ИЗ
	|	Документ.Центр_ТрудозатратыПоЗадаче КАК Центр_ТрудозатратыПоЗадаче
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Центр_ЧасыТрудозатрат КАК Центр_ЧасыТрудозатрат
	|		ПО Центр_ЧасыТрудозатрат.Регистратор = Центр_ТрудозатратыПоЗадаче.Ссылка
	|		И Центр_ТрудозатратыПоЗадаче.Основание = &Задача
	|СГРУППИРОВАТЬ ПО
	|	Центр_ТрудозатратыПоЗадаче.Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ЧасыПоЗадаче.Часы,
	|	Центр_ОценкаЧасовСрезПоследних.До,
	|	Центр_ОценкаЧасовСрезПоследних.ТипОценки
	|ИЗ
	|	вт_ЧасыПоЗадаче КАК вт_ЧасыПоЗадаче
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Центр_ОценкаЧасов.СрезПоследних(, Задача = &Задача) КАК
	|			Центр_ОценкаЧасовСрезПоследних
	|		ПО вт_ЧасыПоЗадаче.Основание = Центр_ОценкаЧасовСрезПоследних.Задача
	|ГДЕ
	|	Центр_ОценкаЧасовСрезПоследних.Согласовано";

	Запрос.УстановитьПараметр("Задача", Основание);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		Если Выборка.ТипОценки <> ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.ПоФакту") Тогда
			ЧасыИтог = 	Выборка.До - Выборка.Часы;
			Если ЧасыИтог < 0 Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Часов больше чем согласовано на " + (-ЧасыИтог) + " часов";
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти