
#Область ОбработчикиСобытий
//@skip-check module-accessibility-at-client
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Автор = ПараметрыСеанса.ТекущийПользователь; 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ПользовательскиеНастройки.Значение,
		|	Центр_ПользовательскиеНастройки.ВидНастройки
		|ИЗ
		|	РегистрСведений.Центр_ПользовательскиеНастройки КАК Центр_ПользовательскиеНастройки
		|ГДЕ
		|	Центр_ПользовательскиеНастройки.Пользователь = &Пользователь
		|	И
		|		Центр_ПользовательскиеНастройки.ВидНастройки.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.НачальноеЗаполнениеЗадачи)
		|	И НЕ Центр_ПользовательскиеНастройки.ВидНастройки.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Пользователь", Автор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ВидНастройки = ПредопределенноеЗначение("ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.ВидЗадачи") Тогда
			ВидЗадачи = Выборка.Значение;	
		КонецЕсли;
		Если Выборка.ВидНастройки = ПредопределенноеЗначение("ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.Постановщик") Тогда
			Постановщик = Выборка.Значение;
			Если ТипЗнч(Постановщик) = Тип("СправочникСсылка.Центр_Партнеры") Тогда
				ГлавныйМенеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Постановщик, "ГлавныйМенеджер");
			КонецЕсли;
		КонецЕсли;
		Если Выборка.ВидНастройки = ПредопределенноеЗначение("ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.Приоритет") Тогда
			Приоритет = Выборка.Значение;
		КонецЕсли;
		Если Выборка.ВидНастройки = ПредопределенноеЗначение("ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.КонтрольВремени") Тогда
			КонтрольВремени = Выборка.Значение;
		КонецЕсли;
		Если Выборка.ВидНастройки = ПредопределенноеЗначение("ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.УчетВремени") Тогда
			УчетВремени = Выборка.Значение;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//@skip-check module-accessibility-at-client
Процедура ОбработкаПроведения(Отказ, РежимПроведения)	
	Центр_ОбработкаТриггеров.ВызовТриггера(Перечисления.Центр_ПодпискаДляТриггера.ПроведениеДокументаЗадача, Ссылка);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ТипЗнч(Постановщик) <> Тип("СправочникСсылка.Центр_Партнеры") Тогда
		Индекс = ПроверяемыеРеквизиты.Найти("ГлавныйМенеджер");
		ПроверяемыеРеквизиты.Удалить(Индекс);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти