
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ДедлайнЗадачСрезПоследних.Задача,
		|	Центр_ДедлайнЗадачСрезПоследних.Согласовано,
		|	Центр_ДедлайнЗадачСрезПоследних.КомментарийОтказа,
		|	Центр_ДедлайнЗадачСрезПоследних.Отказ
		|ПОМЕСТИТЬ вт_Перенос
		|ИЗ
		|	РегистрСведений.Центр_ДедлайнЗадач.СрезПоследних(, Задача = &Задача) КАК Центр_ДедлайнЗадачСрезПоследних
		|ГДЕ
		|	Центр_ДедлайнЗадачСрезПоследних.Согласовант = &Согласовант
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Центр_ОценкаЧасовСрезПоследних.Длительность,
		|	Центр_ОценкаЧасовСрезПоследних.Задача
		|ПОМЕСТИТЬ вт_Длительность
		|ИЗ
		|	РегистрСведений.Центр_ОценкаЧасов.СрезПоследних(, Задача = &Задача) КАК Центр_ОценкаЧасовСрезПоследних
		|ГДЕ
		|	Центр_ОценкаЧасовСрезПоследних.Согласовано
		|	И Центр_ОценкаЧасовСрезПоследних.Согласовант = &Согласовант
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Центр_ДедлайнЗадач.Задача,
		|	КОЛИЧЕСТВО(Центр_ДедлайнЗадач.КомментарийОценки) КАК Количество
		|ПОМЕСТИТЬ вт_СогласованоПоЗадаче
		|ИЗ
		|	РегистрСведений.Центр_ДедлайнЗадач КАК Центр_ДедлайнЗадач
		|ГДЕ
		|	Центр_ДедлайнЗадач.Задача = &Задача
		|	И Центр_ДедлайнЗадач.Согласовано
		|СГРУППИРОВАТЬ ПО
		|	Центр_ДедлайнЗадач.Задача
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Длительность.Длительность,
		|	ЕСТЬNULL(вт_Перенос.Согласовано, ЛОЖЬ) КАК Согласовано,
		|	вт_Перенос.КомментарийОтказа,
		|	ЕСТЬNULL(вт_Перенос.Отказ, ЛОЖЬ) КАК Отказ,
		|	ЕСтьNull(вт_СогласованоПоЗадаче.Количество,0) КАК Количество
		|ИЗ
		|	вт_Длительность КАК вт_Длительность
		|		ПОЛНОЕ СОЕДИНЕНИЕ вт_Перенос КАК вт_Перенос
		|		ПО вт_Перенос.Задача = вт_Длительность.Задача
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_СогласованоПоЗадаче КАК вт_СогласованоПоЗадаче
		|		ПО вт_Длительность.Задача = вт_СогласованоПоЗадаче.Задача";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Согласовант", Пользователи.АвторизованныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Элементы.ПричинаПереноса.Видимость = Ложь;
	Если Выборка.Следующий() Тогда
		Если Выборка.Отказ Тогда
			КомментарийОтказа = Выборка.КомментарийОтказа;
		КонецЕсли;
		Если Выборка.Количество > 0 Тогда
			Элементы.Длительность.Видимость = Ложь;
			Элементы.ДатаСтарта.Видимость = Ложь;
			Элементы.ПричинаПереноса.Видимость = Истина;
			ПереносДедлайна = Истина;
		КонецЕсли;
		Длительность = Выборка.Длительность;
	КонецЕсли;
	ДатаСтарта = ТекущаяДатаСеанса();
	ЧислоСекВДне = 86400;
	Дедлайн = ДатаСтарта + (Длительность * ЧислоСекВДне);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ДатаСтартаПриИзменении(Элемент)
	ЧислоСекВДне = 86400;
	Дедлайн = ДатаСтарта + (Длительность * ЧислоСекВДне);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаСогласование(Команда)
	ОтправитьНаСогласованиеНаСервере();
	Закрыть();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьНаСогласованиеНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_Задача.Постановщик,
		|	Центр_Задача.Постановщик ССЫЛКА Справочник.Центр_Партнеры КАК ЭтоПартнер,
		|	Центр_Задача.ГлавныйМенеджер
		|ИЗ
		|	Документ.Центр_Задача КАК Центр_Задача
		|ГДЕ
		|	Центр_Задача.Ссылка = &Задача";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Согласующий = Неопределено;
	Если Выборка.Следующий() Тогда
		Если Выборка.ЭтоПартнер Тогда
			Согласующий = Выборка.ГлавныйМенеджер;
		Иначе
			Согласующий = Выборка.Постановщик;
		КонецЕсли;
	КонецЕсли;
	Если ПереносДедлайна Тогда
		РегистрыСведений.Центр_ДедлайнЗадач.СоздатьЗаписьСогласования(Задача, Согласующий, Дедлайн, КомментарийОценки,
			ПричинаПереноса);
	Иначе
		РегистрыСведений.Центр_ДедлайнЗадач.СоздатьЗаписьСогласования(Задача, Согласующий, Дедлайн, КомментарийОценки);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти