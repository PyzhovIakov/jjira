 
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ОценкаЧасовСрезПоследних.КомментарийОтказа
		|ИЗ
		|	РегистрСведений.Центр_ОценкаЧасов.СрезПоследних(, Задача = &Задача) КАК Центр_ОценкаЧасовСрезПоследних
		|ГДЕ
		|	Центр_ОценкаЧасовСрезПоследних.Отказ";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		КомментарийОтвет = Выборка.КомментарийОтказа;
	КонецЕсли;
	
	ЕдиницыИзмеренияДлительности = ПредопределенноеЗначение("Перечисление.Центр_ЕдиницыИзмеренийВремени.День");
	ЕдиницыИзмеренияЧасов = ПредопределенноеЗначение("Перечисление.Центр_ЕдиницыИзмеренийВремени.Час");
	ТипОценки = ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.Точная");
	ВидимостьОценкиЧасов();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ТипОценкиПриИзменении(Элемент)
	ВидимостьОценкиЧасов();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаСогласование(Команда)
	ОтправитьНаСогласованиеНаСервере();
	Закрыть();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 &НаСервере
 Процедура ВидимостьОценкиЧасов()
 	Если ТипОценки = ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.Точная") Тогда
 		Элементы.До.Заголовок = "Часы";
 		Элементы.От.Видимость = Ложь;
 		Элементы.До.Видимость = Истина;
 		Элементы.ЕдиницыИзмеренияЧасов.Видимость = Истина;
 	ИначеЕсли ТипОценки = ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.Рамочная") Тогда
 		Элементы.До.Заголовок = "До";
 		Элементы.От.Видимость = Истина;
 		Элементы.До.Видимость = Истина;
 		Элементы.ЕдиницыИзмеренияЧасов.Видимость = Истина;
 	Иначе
 		Элементы.От.Видимость = Ложь;
 		Элементы.До.Видимость = Ложь;
 		Элементы.ЕдиницыИзмеренияЧасов.Видимость = Ложь;
 	КонецЕсли;
 КонецПроцедуры

&НаСервере
Процедура ОтправитьНаСогласованиеНаСервере()
	ДоВРегистр = 0;
	ОтВРегистр = 0;
	ДлительностьВРегистр = 0;
	
	ЧасовВДне = 8;
	ДнейВНеделе = 7;
	Если ЕдиницыИзмеренияДлительности = ПредопределенноеЗначение("Перечисление.Центр_ЕдиницыИзмеренийВремени.Неделя") Тогда
		ДлительностьВРегистр = Длительность * ДнейВНеделе;
	Иначе
		ДлительностьВРегистр = Длительность;
	КонецЕсли;
	
	Если ЕдиницыИзмеренияЧасов = ПредопределенноеЗначение("Перечисление.Центр_ЕдиницыИзмеренийВремени.День") Тогда
		ДоВРегистр = До * ЧасовВДне;
		ОтВРегистр = От * ЧасовВДне;
	Иначе
		ДоВРегистр = До;
		ОтВРегистр = От;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_Задача.Постановщик,
		|	Центр_Задача.Постановщик ССЫЛКА Справочник.Центр_Партнеры КАК ЭтоПартнер,
		|	Центр_Задача.ГлавныйМенеджер
		|ИЗ
		|	Документ.Центр_Задача КАК Центр_Задача
		|ГДЕ
		|	Центр_Задача.Ссылка = &Задача";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Согласующий = Неопределено;
	Если Выборка.Следующий() Тогда
		Если Выборка.ЭтоПартнер Тогда
			Согласующий = Выборка.ГлавныйМенеджер;
		Иначе
			Согласующий = Выборка.Постановщик;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипОценки = ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.Точная") Тогда
 		РегистрыСведений.Центр_ОценкаЧасов.СоздатьЗаписьСогласования(Задача, ТипОценки, ДлительностьВРегистр,
 		КомментарийОценки, Согласующий, , ДоВРегистр);
 	ИначеЕсли ТипОценки = ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.Рамочная") Тогда
 		РегистрыСведений.Центр_ОценкаЧасов.СоздатьЗаписьСогласования(Задача, ТипОценки, ДлительностьВРегистр,
 		КомментарийОценки, Согласующий, ОтВРегистр, ДоВРегистр);
	Иначе		
 		РегистрыСведений.Центр_ОценкаЧасов.СоздатьЗаписьСогласования(Задача, ТипОценки, ДлительностьВРегистр,
 		КомментарийОценки, Согласующий);
 	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти