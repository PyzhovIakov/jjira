
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Центр_ОценкаЧасовСрезПоследних.До,
	|	Центр_ОценкаЧасовСрезПоследних.КомментарийОценки,
	|	Центр_ОценкаЧасовСрезПоследних.Период,
	|	Центр_ОценкаЧасовСрезПоследних.Согласовант,
	|	Центр_ОценкаЧасовСрезПоследних.Длительность,
	|	Центр_ОценкаЧасовСрезПоследних.От,
	|	Центр_ОценкаЧасовСрезПоследних.ТипОценки
	|ИЗ
	|	РегистрСведений.Центр_ОценкаЧасов.СрезПоследних(, Задача = &Задача) КАК Центр_ОценкаЧасовСрезПоследних
	|ГДЕ
	|	НЕ Центр_ОценкаЧасовСрезПоследних.Отказ
	|	И НЕ Центр_ОценкаЧасовСрезПоследних.Согласовано
	|	И Центр_ОценкаЧасовСрезПоследних.Согласующий = &Согласующий";

	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Согласующий", Пользователи.АвторизованныйПользователь());
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		Период = Выборка.Период;
		КомментарийСогласования = Выборка.КомментарийОценки;
		Если Выборка.ТипОценки = ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.Точная") Тогда
			Часы = Строка(Выборка.До);
		ИначеЕсли Выборка.ТипОценки = ПредопределенноеЗначение("Перечисление.Центр_ТипОценкиЧасов.Рамочная") Тогда
			Часы = Строка(Выборка.От) + " - " + Строка(Выборка.До);
		Иначе
			Часы = Строка(Выборка.ТипОценки);
		КонецЕсли;		
		Длительность = Выборка.Длительность;
		Согласовант = Выборка.Согласовант;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Согласовано(Команда)
	СогласованоНаСервере();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отказ(Команда)
	ОповещениеООтмене = Новый ОписаниеОповещения("ОбработкаВопросаОтказ", ЭтотОбъект);
	ПоказатьВводСтроки(ОповещениеООтмене, "", "Введите причину отказа", , Истина);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СогласованоНаСервере()
	РегистрыСведений.Центр_ОценкаЧасов.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопросаОтказ(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ОтказНаСервере(Результат);
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтказНаСервере(Результат)
	РегистрыСведений.Центр_ОценкаЧасов.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Истина, Ложь, Результат);
КонецПроцедуры
#КонецОбласти