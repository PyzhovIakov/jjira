
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Центр_ДедлайнЗадачСрезПоследних.Период,
	|	Центр_ДедлайнЗадачСрезПоследних.Согласовант,
	|	Центр_ДедлайнЗадачСрезПоследних.Дедлайн,
	|	Центр_ДедлайнЗадачСрезПоследних.КомментарийОценки
	|ИЗ
	|	РегистрСведений.Центр_ДедлайнЗадач.СрезПоследних(, Задача = &Задача) КАК Центр_ДедлайнЗадачСрезПоследних
	|ГДЕ
	|	НЕ Центр_ДедлайнЗадачСрезПоследних.Отказ
	|	И НЕ Центр_ДедлайнЗадачСрезПоследних.Согласовано
	|	И Центр_ДедлайнЗадачСрезПоследних.Согласующий = &Согласующий";

	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Согласующий", Пользователи.АвторизованныйПользователь());
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		Период = Выборка.Период;
		КомментарииСогласования = Выборка.КомментарийОценки;	
		Дедлайн = Выборка.Дедлайн;
		Согласовант = Выборка.Согласовант;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Согласовано(Команда)
	СогласованоНаСервере();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отказ(Команда)
	ОповещениеООтмене = Новый ОписаниеОповещения("ОбработкаВопросаОтказ", ЭтотОбъект);
	ПоказатьВводСтроки(ОповещениеООтмене, "", "Введите причину отказа", , Истина);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СогласованоНаСервере()
	РегистрыСведений.Центр_ДедлайнЗадач.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопросаОтказ(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ОтказНаСервере(Результат);
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтказНаСервере(Результат)
	РегистрыСведений.Центр_ДедлайнЗадач.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Истина, Ложь, Результат);
КонецПроцедуры
#КонецОбласти