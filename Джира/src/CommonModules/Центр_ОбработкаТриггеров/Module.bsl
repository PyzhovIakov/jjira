#Область СлужебныеПроцедурыИФункции

Процедура ТриггерСозданиеЗадачи(ЗадачаОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_СтатусыЗадачСрезПоследних.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.Центр_СтатусыЗадач.СрезПоследних(, Задача = &Задача) КАК Центр_СтатусыЗадачСрезПоследних";

	Запрос.УстановитьПараметр("Задача", ЗадачаОбъект);
	
	ВыборкаСтатусЗадачи = Запрос.Выполнить().Выбрать();
	
	Если Не ВыборкаСтатусЗадачи.Следующий() Тогда
		ЗапросСправочник = Новый Запрос;
		ЗапросСправочник.Текст =
		"ВЫБРАТЬ
		|	Центр_ТриггерыПереходыСтатусов.СтатусЗавершения КАК Статус,
		|	Центр_ТриггерыПереходыСтатусов.СтатусСрабатывания
		|ИЗ
		|	Справочник.Центр_Триггеры.ПереходыСтатусов КАК Центр_ТриггерыПереходыСтатусов
		|ГДЕ
		|	Центр_ТриггерыПереходыСтатусов.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	Центр_ТриггерыПереходыСтатусов.СтатусЗавершения,
		|	Центр_ТриггерыПереходыСтатусов.СтатусСрабатывания";
		
		ЗапросСправочник.УстановитьПараметр("Ссылка", Справочники.Центр_Триггеры.СозданиеЗадачи);
		Выборка = ЗапросСправочник.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.Центр_СтатусыЗадач.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = ТекущаяДатаСеанса();
			МенеджерЗаписи.Задача = ЗадачаОбъект;
			МенеджерЗаписи.Статус = Выборка.Статус;
			МенеджерЗаписи.Записать();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ТриггерПросмотрИсполнителем(ЗадачаОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ТриггерыПереходыСтатусов.СтатусСрабатывания,
		|	Центр_ТриггерыПереходыСтатусов.СтатусЗавершения
		|ПОМЕСТИТЬ вт_СтатусыТриггера
		|ИЗ
		|	Справочник.Центр_Триггеры.ПереходыСтатусов КАК Центр_ТриггерыПереходыСтатусов
		|ГДЕ
		|	Центр_ТриггерыПереходыСтатусов.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_СтатусыТриггера.СтатусЗавершения,
		|	ВЫБОР
		|		КОГДА Центр_ИсполнителиЗадачСрезПоследних.Исполнитель = &ТекущийПользователь
		|			ТОГДА Центр_ИсполнителиЗадачСрезПоследних.Исполнитель
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Исполнитель
		|ИЗ
		|	РегистрСведений.Центр_СтатусыЗадач.СрезПоследних(, Задача = &Задача) КАК Центр_СтатусыЗадачСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_СтатусыТриггера КАК вт_СтатусыТриггера
		|		ПО Центр_СтатусыЗадачСрезПоследних.Статус = вт_СтатусыТриггера.СтатусСрабатывания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Центр_ИсполнителиЗадач.СрезПоследних(, Задача = &Задача) КАК
		|			Центр_ИсполнителиЗадачСрезПоследних
		|		ПО Центр_ИсполнителиЗадачСрезПоследних.Задача = Центр_СтатусыЗадачСрезПоследних.Задача";
	
	Запрос.УстановитьПараметр("Ссылка", Справочники.Центр_Триггеры.ОзнакомлениеИсполнителя);
	Запрос.УстановитьПараметр("Задача", ЗадачаОбъект);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Исполнитель) И ЗначениеЗаполнено(Выборка.СтатусЗавершения) Тогда
		МенеджерЗаписи = РегистрыСведений.Центр_СтатусыЗадач.СоздатьМенеджерЗаписи();
        МенеджерЗаписи.Период = ТекущаяДатаСеанса();
        МенеджерЗаписи.Задача = ЗадачаОбъект;
        МенеджерЗаписи.Статус = Выборка.СтатусЗавершения;
        МенеджерЗаписи.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ТриггерВзятиеЗадачи(Задача) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
                 |    Центр_СтатусыЗадачСрезПоследних.Статус
                 |ПОМЕСТИТЬ вт_ТекущийСтатус
                 |ИЗ
                 |    РегистрСведений.Центр_СтатусыЗадач.СрезПоследних КАК Центр_СтатусыЗадачСрезПоследних
                 |ГДЕ
                 |    Центр_СтатусыЗадачСрезПоследних.Задача = &Задача
                 |;
                 |
                 |////////////////////////////////////////////////////////////////////////////////
                 |ВЫБРАТЬ
                 |    Центр_ТриггерыПереходыСтатусов.СтатусЗавершения как Статус
                 |ИЗ
                 |    вт_ТекущийСтатус КАК вт_ТекущийСтатус
                 |        ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Центр_Триггеры.ПереходыСтатусов КАК Центр_ТриггерыПереходыСтатусов
                 |        ПО Центр_ТриггерыПереходыСтатусов.Ссылка = &Ссылка
                 |        И вт_ТекущийСтатус.Статус = Центр_ТриггерыПереходыСтатусов.СтатусСрабатывания";
    Запрос.УстановитьПараметр("Задача", Задача);
    Запрос.УстановитьПараметр("Ссылка", ПредопределенноеЗначение("Справочник.Центр_Триггеры.ВзятиеЗадачи"));
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Пока Выборка.Следующий() Цикл
        МенеджерЗаписи = РегистрыСведений.Центр_СтатусыЗадач.СоздатьМенеджерЗаписи();
        МенеджерЗаписи.Период = ТекущаяДатаСеанса();
        МенеджерЗаписи.Задача = Задача;
        МенеджерЗаписи.Статус = Выборка.Статус;
        МенеджерЗаписи.Записать();
    КонецЦикла;

КонецПроцедуры

Процедура ТриггерЗакрытиеЗадачи(Задача) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
                 |    Центр_СтатусыЗадачСрезПоследних.Статус
                 |ПОМЕСТИТЬ вт_ТекущийСтатус
                 |ИЗ
                 |    РегистрСведений.Центр_СтатусыЗадач.СрезПоследних КАК Центр_СтатусыЗадачСрезПоследних
                 |ГДЕ
                 |    Центр_СтатусыЗадачСрезПоследних.Задача = &Задача
                 |;
                 |
                 |////////////////////////////////////////////////////////////////////////////////
                 |ВЫБРАТЬ
                 |    Центр_ТриггерыПереходыСтатусов.СтатусЗавершения как Статус
                 |ИЗ
                 |    вт_ТекущийСтатус КАК вт_ТекущийСтатус
                 |        ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Центр_Триггеры.ПереходыСтатусов КАК Центр_ТриггерыПереходыСтатусов
                 |        ПО Центр_ТриггерыПереходыСтатусов.Ссылка = &Ссылка
                 |        И вт_ТекущийСтатус.Статус = Центр_ТриггерыПереходыСтатусов.СтатусСрабатывания";
    Запрос.УстановитьПараметр("Задача", Задача);
    Запрос.УстановитьПараметр("Ссылка", ПредопределенноеЗначение("Справочник.Центр_Триггеры.ЗакрытиеЗадачи"));
    РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();

    Пока Выборка.Следующий() Цикл
        МенеджерЗаписи = РегистрыСведений.Центр_СтатусыЗадач.СоздатьМенеджерЗаписи();
        МенеджерЗаписи.Период = ТекущаяДатаСеанса();
        МенеджерЗаписи.Задача = Задача;
        МенеджерЗаписи.Статус = Выборка.Статус;
        МенеджерЗаписи.Записать();
    КонецЦикла;

КонецПроцедуры

Процедура ТриггерВопросПоЗадаче(ЗадачаОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ТриггерыПереходыСтатусов.СтатусСрабатывания,
		|	Центр_ТриггерыПереходыСтатусов.СтатусЗавершения
		|ПОМЕСТИТЬ вт_СтатусыТриггера
		|ИЗ
		|	Справочник.Центр_Триггеры.ПереходыСтатусов КАК Центр_ТриггерыПереходыСтатусов
		|ГДЕ
		|	Центр_ТриггерыПереходыСтатусов.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_СтатусыТриггера.СтатусЗавершения,
		|	Центр_ИсполнителиЗадачСрезПоследних.Исполнитель,
		|	Центр_ИсполнителиЗадачСрезПоследних.Задача
		|ПОМЕСТИТЬ вт_СостояниеЗадачи
		|ИЗ
		|	РегистрСведений.Центр_СтатусыЗадач.СрезПоследних(, Задача = &Задача) КАК Центр_СтатусыЗадачСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_СтатусыТриггера КАК вт_СтатусыТриггера
		|		ПО Центр_СтатусыЗадачСрезПоследних.Статус = вт_СтатусыТриггера.СтатусСрабатывания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Центр_ИсполнителиЗадач.СрезПоследних(, Задача = &Задача) КАК
		|			Центр_ИсполнителиЗадачСрезПоследних
		|		ПО Центр_ИсполнителиЗадачСрезПоследних.Задача = Центр_СтатусыЗадачСрезПоследних.Задача
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_СостояниеЗадачи.СтатусЗавершения,
		|	Центр_ВопросыПоЗадачам.Адресат,
		|	МАКСИМУМ(Центр_ВопросыПоЗадачам.Дата) КАК Дата
		|ИЗ
		|	РегистрСведений.Центр_ВопросыПоЗадачам КАК Центр_ВопросыПоЗадачам
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_СостояниеЗадачи КАК вт_СостояниеЗадачи
		|		ПО вт_СостояниеЗадачи.Исполнитель = Центр_ВопросыПоЗадачам.Отправитель
		|ГДЕ
		|	Центр_ВопросыПоЗадачам.ВидВопроса = ЗНАЧЕНИЕ(Перечисление.Центр_ВидыВопросов.Вопрос)
		|СГРУППИРОВАТЬ ПО
		|	вт_СостояниеЗадачи.СтатусЗавершения,
		|	Центр_ВопросыПоЗадачам.Адресат";
	
	Запрос.УстановитьПараметр("Ссылка", Справочники.Центр_Триггеры.ВопросПоЗадаче);
	Запрос.УстановитьПараметр("Задача", ЗадачаОбъект);	
	РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Адресат) И ЗначениеЗаполнено(Выборка.СтатусЗавершения) Тогда
		МенеджерЗаписи = РегистрыСведений.Центр_СтатусыЗадач.СоздатьМенеджерЗаписи();
        МенеджерЗаписи.Период = ТекущаяДатаСеанса();
        МенеджерЗаписи.Задача = ЗадачаОбъект;
        МенеджерЗаписи.Статус = Выборка.СтатусЗавершения;
        МенеджерЗаписи.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура ТриггерОтветНаВопросПоЗадаче(ЗадачаОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ТриггерыПереходыСтатусов.СтатусСрабатывания,
		|	Центр_ТриггерыПереходыСтатусов.СтатусЗавершения
		|ПОМЕСТИТЬ вт_СтатусыТриггера
		|ИЗ
		|	Справочник.Центр_Триггеры.ПереходыСтатусов КАК Центр_ТриггерыПереходыСтатусов
		|ГДЕ
		|	Центр_ТриггерыПереходыСтатусов.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_СтатусыТриггера.СтатусЗавершения
		|ПОМЕСТИТЬ вт_СостояниеЗадачи
		|ИЗ
		|	РегистрСведений.Центр_СтатусыЗадач.СрезПоследних(, Задача = &Задача) КАК Центр_СтатусыЗадачСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_СтатусыТриггера КАК вт_СтатусыТриггера
		|		ПО Центр_СтатусыЗадачСрезПоследних.Статус = вт_СтатусыТриггера.СтатусСрабатывания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|		КОГДА Центр_ВопросыПоЗадачам.ТекстВопроса ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ) КАК КоличествоВопросов
		|ПОМЕСТИТЬ вт_КолВоВопросов
		|ИЗ
		|	РегистрСведений.Центр_ВопросыПоЗадачам КАК Центр_ВопросыПоЗадачам
		|ГДЕ
		|	Центр_ВопросыПоЗадачам.ВидВопроса = ЗНАЧЕНИЕ(Перечисление.Центр_ВидыВопросов.Вопрос)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|		КОГДА Центр_ВопросыПоЗадачам.ТекстВопроса ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ) КАК КоличествоОтветов
		|ПОМЕСТИТЬ вт_КоличествоОтветов
		|ИЗ
		|	РегистрСведений.Центр_ВопросыПоЗадачам КАК Центр_ВопросыПоЗадачам
		|ГДЕ
		|	Центр_ВопросыПоЗадачам.ВидВопроса = ЗНАЧЕНИЕ(Перечисление.Центр_ВидыВопросов.Ответ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(вт_КоличествоОтветов.КоличествоОтветов, 0) КАК КоличествоОтветов,
		|	вт_СостояниеЗадачи.СтатусЗавершения,
		|	ЕСТЬNULL(вт_КолВоВопросов.КоличествоВопросов, 0) КАК КоличествоВопросов
		|ИЗ
		|	вт_КолВоВопросов КАК вт_КолВоВопросов,
		|	вт_КоличествоОтветов КАК вт_КоличествоОтветов,
		|	вт_СостояниеЗадачи КАК вт_СостояниеЗадачи";
	
	Запрос.УстановитьПараметр("Ссылка", Справочники.Центр_Триггеры.ОтветНаВопросПоЗадаче);
	Запрос.УстановитьПараметр("Задача", ЗадачаОбъект);	
	РезультатЗапроса = Запрос.Выполнить();
    Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.КоличествоОтветов) И ЗначениеЗаполнено(Выборка.КоличествоВопросов) И ЗначениеЗаполнено(Выборка.СтатусЗавершения) И Выборка.КоличествоОтветов = Выборка.КоличествоВопросов Тогда
		МенеджерЗаписи = РегистрыСведений.Центр_СтатусыЗадач.СоздатьМенеджерЗаписи();
        МенеджерЗаписи.Период = ТекущаяДатаСеанса();
        МенеджерЗаписи.Задача = ЗадачаОбъект;
        МенеджерЗаписи.Статус = Выборка.СтатусЗавершения;
        МенеджерЗаписи.Записать();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти