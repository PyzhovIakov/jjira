#Область СлужебныеПроцедурыИФункции

Процедура ВызовТриггера(Подписка, Задача = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_Триггеры.Запрос,
		|	Центр_Триггеры.Ссылка,
		|	Центр_Триггеры.Уведомлять,
		|	Центр_Триггеры.Статус,
		|	Центр_Триггеры.Наименование
		|ПОМЕСТИТЬ вт_Триггер
		|ИЗ
		|	Справочник.Центр_Триггеры КАК Центр_Триггеры
		|ГДЕ
		|	Центр_Триггеры.Подписка = &Подписка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Триггер.Запрос КАК Запрос,
		|	вт_Триггер.Ссылка КАК Ссылка,
		|	вт_Триггер.Уведомлять КАК Уведомлять,
		|	вт_Триггер.Статус,
		|	Центр_ТриггерыПараметры.Параметр,
		|	Центр_ТриггерыПараметры.Значение,
		|	вт_Триггер.Наименование КАК Наименование
		|ИЗ
		|	вт_Триггер КАК вт_Триггер
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Центр_Триггеры.Параметры КАК Центр_ТриггерыПараметры
		|		ПО вт_Триггер.Ссылка = Центр_ТриггерыПараметры.Ссылка
		|ИТОГИ
		|	МАКСИМУМ(Запрос) КАК Запрос,
		|	МАКСИМУМ(Уведомлять) КАК Уведомлять,
		|	МАКСИМУМ(Наименование) КАК Наименование
		|ПО
		|	Ссылка";
	Запрос.УстановитьПараметр("Подписка", Подписка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		Попытка
			 ВыполнениеЗапросаИзСправочника(Выборка, Задача);		
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Ошибка Выполнения");
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

Процедура ВыполнениеЗапросаИзСправочника(СправочникКонтроля, Задача)
	Запрос = Новый Запрос;
	Запрос.Текст = СправочникКонтроля.Запрос;
	ВыборкаПараметровКонтроля = СправочникКонтроля.Выбрать();
	Пока ВыборкаПараметровКонтроля.Следующий() Цикл
		Запрос.УстановитьПараметр(ВыборкаПараметровКонтроля.Параметр, ВыборкаПараметровКонтроля.Значение);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
	Запрос.УстановитьПараметр("Задача", Задача);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекущаяДата = ТекущаяДатаСеанса();
		МенеджерЗаписи = РегистрыСведений.Центр_СтатусыЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = ТекущаяДата;
		МенеджерЗаписи.Задача = Выборка.Ссылка;
		МенеджерЗаписи.Статус = СправочникКонтроля.Статус;
		МенеджерЗаписи.Записать();
		Если СправочникКонтроля.Уведомлять Тогда
			Центр_УведомленияПользователей.ОтправитьУведомлениеПользователю(Выборка.Пользователь,
				СправочникКонтроля.Наименование, ТекущаяДата, ТекущаяДата,
				Перечисления.СпособыУстановкиВремениНапоминания.Периодически, 60, Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти