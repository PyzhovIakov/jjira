#Область ПрограммныйИнтерфейс
#Область ЗаполнениеПолей
//Заполняет поля формы по настройкам пользователя
//Параметры: 
// ЭтаФорма - Форма
Процедура ЗаполнитьПользовательскиеНастройки(ЭтаФорма) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ПользовательскиеНастройки.Значение,
		|	Центр_ПользовательскиеНастройки.ВидНастройки
		|ИЗ
		|	РегистрСведений.Центр_ПользовательскиеНастройки КАК Центр_ПользовательскиеНастройки
		|ГДЕ
		|	Центр_ПользовательскиеНастройки.Пользователь = &Пользователь
		|	И
		|		Центр_ПользовательскиеНастройки.ВидНастройки.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.НачальноеЗаполнениеЗадачи)
		|	И НЕ Центр_ПользовательскиеНастройки.ВидНастройки.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Выборка.ВидНастройки = ПредопределенноеЗначение(
			"ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.Исполнитель") Тогда
			ЭтаФорма.Исполнитель = Выборка.Значение;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//Заполняет поля формы по настройкам пользователя
//Возвращаемое значение:
//	Булево
Функция ЕстьПользовательскиеНастройки() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_ПользовательскиеНастройки.Значение,
		|	Центр_ПользовательскиеНастройки.ВидНастройки
		|ИЗ
		|	РегистрСведений.Центр_ПользовательскиеНастройки КАК Центр_ПользовательскиеНастройки
		|ГДЕ
		|	Центр_ПользовательскиеНастройки.Пользователь = &Пользователь
		|	И
		|		Центр_ПользовательскиеНастройки.ВидНастройки.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.Центр_ВидыПользовательскихНастроек.НачальноеЗаполнениеЗадачи)
		|	И НЕ Центр_ПользовательскиеНастройки.ВидНастройки.ЭтоГруппа
		|	И Центр_ПользовательскиеНастройки.Значение <> НЕОПРЕДЕЛЕНО";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.АвторизованныйПользователь());
	
	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

//Заполняет данные объекта из источника
//Параметры: 
// ЭтаФорма - Форма
// Источник - ДокументСсылка.Центр_Задача
Процедура ЗаполнитьНаследуемыеПараметры(ЭтаФорма, Источник) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Центр_Задача.ВидЗадачи,
		|	Центр_Задача.Постановщик КАК Постановщик,
		|	Центр_Задача.УчетВремени,
		|	Центр_Задача.КонтрольВремени,
		|	Центр_Задача.Приоритет,
		|	Центр_Задача.ГлавныйМенеджер
		|ИЗ
		|	Документ.Центр_Задача КАК Центр_Задача
		|ГДЕ
		|	Центр_Задача.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Центр_ЗадачаНаблюдатели.Пользователь
		|ИЗ
		|	Документ.Центр_Задача.Наблюдатели КАК Центр_ЗадачаНаблюдатели
		|ГДЕ
		|	Центр_ЗадачаНаблюдатели.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаОсновныеПараметры = РезультатЗапроса[0].Выбрать();
	Если ВыборкаОсновныеПараметры.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма.Объект, ВыборкаОсновныеПараметры);
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаОсновныеПараметры.ВидЗадачи, "Партнер") Тогда
			ЭтаФорма.Объект.Постановщик = ВыборкаОсновныеПараметры.Постановщик;
			ЭтаФорма.Исполнитель = ВыборкаОсновныеПараметры.ГлавныйМенеджер;
		Иначе
			ЭтаФорма.Объект.Постановщик = Пользователи.АвторизованныйПользователь();
			ЭтаФорма.Исполнитель = ВыборкаОсновныеПараметры.Постановщик;
		КонецЕсли
	КонецЕсли;
	
	ВыборкаНаблюдатели = РезультатЗапроса[1].Выбрать();
	Пока ВыборкаНаблюдатели.Следующий() Цикл
		НоваяСтрокаНаблюдателей = ЭтаФорма.Объект.Наблюдатели.Добавить();
		НоваяСтрокаНаблюдателей.Пользователь = ВыборкаНаблюдатели.Пользователь;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти
#КонецОбласти